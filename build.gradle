/*
 *
 * Copyright (c) 2018 PAY, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.4.0"
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint("0.38.1")
            licenseHeaderFile project.rootProject.file('spotless/Copyright.kt')
        }
    }
}

ext {

    minSdkVersion = 16
    sdkVersion = 29

    okHttp = "3.12.12"
    moshi = "1.9.3"
    retrofit = "2.6.4"
    lifecycle = "2.2.0"
    mockito = "2.28.2"
    coroutine = "1.3.7"
    appcompat = "1.1.0"
    fragment = "1.2.5"
    material = "1.2.0-beta01"
    constraint = "2.0.0-beta7"
    recyclerview = "1.1.0"
    cardio = "5.5.1"

    isCi = "true" == System.getenv("CI")
    preDexEnabled = "true" == System.getProperty("pre-dex", "true")
    numberOfCommits = numberOfCommits()
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/groups/public/" }
    }
}

def numberOfCommits() {
    def p = 'git rev-list HEAD --count'.execute([], project.rootDir)
    if (p.waitFor() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }
    return p.text.trim().toInteger()
}
