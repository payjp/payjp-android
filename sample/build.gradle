/*
 *
 * Copyright (c) 2018 PAY, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.appdistribution'

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:1.3.1'
    }
}

android {
    compileSdkVersion rootProject.sdkVersion

    defaultConfig {
        applicationId "com.example.payjp.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.numberOfCommits
        versionName VERSION_NAME

        firebaseAppDistribution {
            groups="internal-tester"
        }
    }

    sourceSets.all {
        java.srcDirs += "src/${name}/kotlin"
    }

    // for check release build
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable true
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }
}

dependencies {

    implementation project(':payjp-android-main')
    implementation project(':payjp-android-cardio')
    implementation project(':payjp-android-coroutine')
//    implementation "jp.pay:payjp-android:$VERSION_NAME"
//    implementation "jp.pay:payjp-android-cardio:$VERSION_NAME"
//    implementation "jp.pay:payjp-android-coroutine:$VERSION_NAME"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.recyclerview:recyclerview:$recyclerview"
    implementation "com.google.android.material:material:$material"

    // card scan
    implementation "io.card:android-sdk:$cardio"

    // api
    implementation "com.squareup.okhttp3:okhttp:$okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp"
    implementation "com.squareup.moshi:moshi:$moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"

    implementation 'com.google.android.gms:play-services-base:17.3.0'

    // debug
    implementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-5'
}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
    // Remove google services plugin if credential does not exists.
    project.tasks.each { t ->
        if (t.name.toLowerCase().contains("googleservices")) {
            t.enabled = project.file("google-services.json").exists()
        }
    }
}